@echo off
setlocal

SET ERRORLEVEL=0
CALL "%~dp0util\util.bat" :showBanner

echo Data Loader installation requires you to provide an installation directory to create a version-specific subdirectory for the installation artifacts.
echo It uses '%USERPROFILE%\^<relative path^>' as the installation directory if you provide a relative path for the installation directory.
echo.
set /p "DIR_NAME=Provide the installation directory [default: dataloader]" : || set DIR_NAME=dataloader

if "%DIR_NAME:~1,1%" == ":" (
    REM absolute path specified
    set "INSTALLATION_DIR=%DIR_NAME%\v%DATALOADER_VERSION%"
) else (
    if "%DIR_NAME:~0,1%" == "\" (
        REM absolute path specified
        set "INSTALLATION_DIR=%DIR_NAME%\v%DATALOADER_VERSION%"
    ) else (
        REM relative path specified
        set "INSTALLATION_DIR=%USERPROFILE%\%DIR_NAME%\v%DATALOADER_VERSION%"
    )
)

IF EXIST "%INSTALLATION_DIR%" (
    call :deleteExistingDir
)
if "%ERRORLEVEL%" == "0" (
    call :copyFilesToInstallationDir
) ELSE (
    echo.
    echo Did not overwrite currently installed data loader v%DATALOADER_VERSION%. You can re-run installation using another directory.
)
goto :exit

:deleteExistingDir
    set prompt="Do you want to overwrite currently installed Data Loader v%DATALOADER_VERSION% and its configuration in '%INSTALLATION_DIR%'?[Yes/No]"
    CALL :promptAndExecuteOperation %prompt% doDeleteExistingDir 1
    EXIT /b %ERRORLEVEL%

:copyFilesToInstallationDir
    echo.
    set "SRC_DIR=%~dp0"
    IF %SRC_DIR:~-1%==\ SET "SRC_DIR=%SRC_DIR:~0,-1%"
    echo Copying files from '%SRC_DIR%' to '%INSTALLATION_DIR%'  ...
    xcopy "%SRC_DIR%" "%INSTALLATION_DIR%" /e /i
    del "%INSTALLATION_DIR%\install.bat" /q
    echo Your Data Loader v%DATALOADER_VERSION% is created in '%INSTALLATION_DIR%'
    CALL :promptForShortcut StartMenu
    CALL :promptForShortcut Desktop
    EXIT /b 0
    
:promptForShortcut
REM Usage:
REM CALL :promptForShortcut <desktop | startMenu>
    set createLabel=create%~1Shortcut
    set desktopShortcutPrompt="Would you like to create a desktop icon? [Yes/No]"
    set startMenuShortcutPrompt="Would you like to create a start menu shortcut? [Yes/No]"
    set prompt=%startMenuShortcutPrompt%
    IF /I "%~1" == "desktop" (
        set prompt=%desktopShortcutPrompt%
    )
    CALL :promptAndExecuteOperation %prompt% %createLabel% 0
    EXIT /b %ERRORLEVEL%
    
:promptAndExecuteOperation
    SET executeOperation=""
    echo.
    SET /p REPLY=%~1
    if /I "%REPLY%"=="Y" SET executeOperation="true"
    if /I "%REPLY%"=="Yes" SET executeOperation="true"
    if /I "%REPLY%"=="N" SET executeOperation="false"
    if /I "%REPLY%"=="No" SET executeOperation="false"
    if %executeOperation%=="true" (
      CALL "%~dp0util\util.bat" :%~2 "%INSTALLATION_DIR%"
      EXIT /b %ERRORLEVEL%
    )
    if %executeOperation%=="false" (
      SET ERRORLEVEL=%~3
      EXIT /b %ERRORLEVEL%
    )
    echo Type Yes or No.
    goto :promptAndExecuteOperation
    EXIT /b %ERRORLEVEL%

:exit
    echo.
    echo Data Loader installation is quitting.
    endlocal
    PAUSE
    EXIT %ERRORLEVEL%